// This class can be replaced by the class "MetricCPP"
// if no smoothing of the cepstrum is desired.
// Just rename this source code file to "MetricCPPs.sc.hidden",
// and rename the file "MetricCPP.sc.hidden" to "MetricCPP.sc".
// If there arises a class mismatch between existing data files and the class library,
// existing _VRP.csv files will be displayed with a blank CPPs layer,
// and existing _cPhon.csv files will ignore the CPPs metric.

MetricCPP : VRPMetric {
	classvar <metricNumber;		// the desired index of this metric
	classvar <symbol = \CPPs;
	classvar <busName = \CPPsmoothed;
	classvar <busRate = \control;
	var fnMyPalette;

	*new { arg number=nil, bDifference=false, file=nil;
		^super.new.init(number, bDifference, file);
	}

	init { arg number, bDiff=false, file;
		metricNumber = number ? VRPSettings.icppSmoothed;
		bDifferencing = bDiff;
		super.init(file);
		if (file.notNil,
			{ this.configFromFile(file) },
			{ this.configFromCode() }
		);
		// map CPP 0...+20 (dB) to minColor...maxColor
		minVal = 0.0;
		maxVal = 20.0;						// 30 is too high when smoothed

		fnMyPalette = { | v |
			var cHue = v.linlin(minVal, maxVal, 0.666, 0.0);
			Color.hsv(cHue, 1, 1);
		};
	}

	fnStandardizeMsg {
		// updates these settings when called
		^msgStandardize = ['linlin', rangeLow, rangeHigh, 0, 1]
	}

	configFromCode {
		csvName = "CPPs";	// Column title in map files

		menuText = "Audio CPPs";		// String for the layer menu
		colorBarWarpType = \lin;				// Color axis \lin or \exp
		unit = "dB";							// Unit, if applicable, e.g. "dB"

		if (bDifferencing.not, {
			// Color mapping of value
			rangeLow = minVal;
			rangeHigh = maxVal;
			msgStandardize = ['linlin', rangeLow, rangeHigh, 0, 1 ];
			palette = this.getColorPalette(fnMyPalette);
			colorBarText = "Mean CPPs";		// String for the color bar
			trendText = "→ Peakier cepstrum";  // String for info text
			trendTextColor = palette.(maxVal);
		} , {
			// Color mapping of delta-value
			// map CPPs difference -10...+10 (dB) to red...green
			minVal = -10;
			maxVal = 10.01;
			palette = this.getColorPaletteDiff;
			trendText = "→ Higher CPPs";  // String for info text
			colorBarText = "CPPs diff"; 			// Ditto, when differencing
			trendTextColor = palette.(maxVal);
		});
	}
}

